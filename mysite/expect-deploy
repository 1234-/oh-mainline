proc status {msg} {send_user "\033\[1;32m$msg\033\[0m"}
proc status_error {msg} {send_user "\033\[1;31m$msg\033\[0m"}
proc status_warning {msg} {send_user "\033\[1;33m$msg\033\[0m"}

# Log into linode
status "\r\n*** Dialing into remote computer... ***\r\n"
spawn ssh deploy@linode.openhatch.org
expect "$ "

# Make the remote shell exit on error.
send "set -e\r"
expect "$ "

send "cd milestone-a\r"
expect "$ "

status "\r\n*** Fetching changes from git's remote repository... ***\r\n"
send "git fetch\r"
expect "$ "

status "\r\n*** Checking for uncommitted modifications to the remote filesystem... ***\r\n"
send "git ls-files -m\r"
expect "$ " {exp_continue} \
           ".*" {status_error "\r\nThere were some uncommitted modifications in the remote filesystem.\r\n"; exit 3}

status "\r\n*** Merging... ***\r\n"
send "git merge origin/master\r"

while {true} {
    expect {
        "Fast forward" {
            status "Fast forward detected. Proceeding...\r"

            # Run buildout if buildout was changed.
            expect {
                "buildout.cfg" {
                    expect "$ "
                    status "\r\n*** Change to buildout.cfg detected. Running buildout... ***\r\n"
                    send "./bin/buildout\r"
                    expect "$ "
                }
                "$ "
            }
            break
        }
        "Already up-to-date." {
            status_warning "Tried to merge, but git cried, 'Already up-to-date'. Proceeding anyway...\r\n"
            expect "$ "
            break
        }
        "$ " {
            status_error "There was no fast forward. Aborting...\r\n"
            exit 1
        }
    }
}

status "\r\n*** Attempting to run migrations... ***\r\n"
send "./bin/production migrate --merge\r"
expect "$ "

status "\r\n*** Syncing the un-migrated models... ***\r\n"
send "./bin/production syncdb\r"
expect "$ "

status "\r\n*** Reloading the web site... ***\r\n"
send "touch bin/production.wsgi\r"
expect "$ "

send "exit\r"
expect eof
